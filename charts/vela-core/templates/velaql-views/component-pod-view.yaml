apiVersion: v1
kind: ConfigMap
metadata:
  name: component-pod-view
  namespace: {{.Values.systemDefinitionNamespace}}
data:
  template: |
    import (
      "vela/ql"
      "vela/op"
      "strings"
    )

    parameter: {
      appName:    string
      appNs:      string
      name:       string
      cluster?:   string
      clusterNs?: string
    }

    appList: ql.#ListResourcesInApp & {
      app: {
        name:      parameter.appName
        namespace: parameter.appNs
        components: [parameter.name]
        filter: {
          if parameter.cluster != _|_ {
            cluster: parameter.cluster
          }
          if parameter.clusterNs != _|_ {
            clusterNamespace: parameter.clusterNs
          }
        }
      }
    }

    if appList.err == _|_ {
      appRev:            appList.list[0].revision
      appPublishVersion: appList.list[0].publishVersion
      appDeployVersion:  appList.list[0].deployVersion
      resources:         appList.list[0].components[0].resources
      collectedPods:     op.#Steps & {
        for i, resource in resources {
          "\(i)": ql.#CollectPods & {
            value:   resource.object
            cluster: resource.cluster
          }
        }
      }
      podsWithCluster: [ for pods in collectedPods if pods.list != _|_ for podObj in pods.list {
        cluster: pods.cluster
        obj:     podObj
        workload: {
          apiVersion: pods.value.apiVersion
          kind:       pods.value.kind
        }
      }]
      podsError: [ for pods in collectedPods if pods.err != _|_ {pods.err}]
      status: {
        if len(podsError) == 0 {
          podList: [ for pod in podsWithCluster {
            cluster: pod.cluster
            workload: pod.workload
            metadata: {
              name:         pod.obj.metadata.name
              namespace:    pod.obj.metadata.namespace
              creationTime: pod.obj.metadata.creationTimestamp
              version: {
                revision:       appRev
                publishVersion: appPublishVersion
                deployVersion:  appDeployVersion
              }
            }
            status: {
              phase: pod.obj.status.phase
              // refer to https://kubernetes.io/docs/concepts/workloads/pods/pod-lifecycle/#pod-phase
              if phase != "Pending" && phase != "Unknown" {
                podIP:    pod.obj.status.podIP
                hostIP:   pod.obj.status.hostIP
                nodeName: pod.obj.spec.nodeName
              }
            }
          }]
        }
        if len(podsError) != 0 {
          error: strings.Join(podsError, ",")
        }
      }
    }

    if appList.err != _|_ {
      status: {
        error: appList.err
      }
    }
